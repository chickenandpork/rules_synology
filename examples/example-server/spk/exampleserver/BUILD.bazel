load("@bazel_skylib//rules:copy_file.bzl", "copy_file")
load("@rules_synology//:defs.bzl", "REQUIRED_SCRIPTS", "images", "info_file", "maintainer", "protocol_file", "resource_config", "service_config")
load("@rules_pkg//pkg:mappings.bzl", "pkg_attributes", "pkg_filegroup", "pkg_files", "pkg_mkdirs", "strip_prefix")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")

info_file(
    name = "info",
    package_name = "exampleserver",
    description = "A basic example of a TCP service running on a static port",
    maintainer = "//maintainers:chickenandpork",
    os_min_ver = "7.0",
    package_version = "1.0.0-1",
)

service_config(
    name = "exampleserver-main",
    description = "Example Server",
    dst_ports = "39267/tcp",
    title = "Example Server Service Port",
)  # E-X-A-M-P on a dialpad == 39267

protocol_file(
    name = "protocol",
    package_name = "exampleserver",
    service_config = [":exampleserver-main"],
)

resource_config(
    name = "rez",
    resources = [":protocol"],
)

# Create icon images
images(
    name = "icons",
    src = "@screaming_goat//file",
)

pkg_files(
    name = "conf",
    srcs = [
        ":rez",
    ],
    attributes = pkg_attributes(
        mode = "0444",
    ),
    prefix = "conf",
    visibility = ["//visibility:public"],
)

pkg_tar(
    name = "package",
    srcs = [
        "//server",
    ],
    extension = "tgz",
    package_dir = "/usr/bin",
)

SCRIPTS = [
    "preinst",
    "postinst",
    "preuninst",
    "postuninst",
    "preupgrade",
    "postupgrade",
    "start-stop-status",
]

[copy_file(
    name = "stub_{}".format(f),
    src = "@rules_synology//synology:stub_script",
    out = f,
) for f in SCRIPTS]

pkg_files(
    name = "scripts",
    srcs = [":stub_{}".format(f) for f in SCRIPTS],
    attributes = pkg_attributes(
        mode = "0755",
    ),
    prefix = "scripts",
)

pkg_tar(
    name = "spk",
    srcs = [
        ":conf",
        ":icons.group",
        ":info",
        ":package",
        ":scripts",
        "@rules_pkg//pkg:verify_archive_test_main.py.tpl",
    ],
    extension = "tar",
    package_file_name = "exampleserver.spk",
    visibility = ["//visibility:public"],
)

#copy_file(name = "exampleserver", src=":spk", out="exampleserver.spk")
