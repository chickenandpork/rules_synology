load("@bazel_skylib//rules:copy_file.bzl", "copy_file")
load("@rules_pkg//pkg:mappings.bzl", "pkg_attributes", "pkg_filegroup", "pkg_files", "pkg_mkdirs", "strip_prefix")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("@rules_synology//:defs.bzl", "SPK_REQUIRED_SCRIPTS", "images", "info_file", "maintainer", "privilege_config", "protocol_file", "resource_config", "service_config")

info_file(
    name = "info",
    package_name = "exampleserver",
    arch_strings = ["denverton"],
    description = "A basic example of a TCP service running on a static port",
    maintainer = "//maintainers:chickenandpork",
    os_min_ver = "7.0-1",  # correct-format=[^\d+(\.\d+){1,2}(-\d+){1,2}$]
    package_version = "1.0.0-1",
)

service_config(
    name = "exampleserver-main",
    description = "Example Server",
    dst_ports = "39267/tcp",
    title = "Example Server Service Port",
)  # E-X-A-M-P on a dialpad == 39267

protocol_file(
    name = "protocol",
    package_name = "exampleserver",
    service_config = [":exampleserver-main"],
)

privilege_config(
    name = "priv",
)

resource_config(
    name = "rez",
    resources = [":protocol"],
)

# Create icon images
images(
    name = "icons",
    src = "@screaming_goat//file",
)

pkg_files(
    name = "conf",
    srcs = [
        ":priv",
        ":rez",
    ],
    attributes = pkg_attributes(
        mode = "0444",
    ),
    prefix = "conf",
    visibility = ["//visibility:public"],
)

pkg_tar(
    name = "package",
    srcs = [
        ":protocol",
    ],
    extension = "tgz",
    package_dir = "/",
    deps = [":package-bin"],
)

pkg_tar(
    name = "package-bin",
    srcs = [
        "//server",
    ],
    extension = "tgz",
    package_dir = "/usr/bin",
)

[copy_file(
    name = "stub_{}".format(f),
    src = "@rules_synology//synology:stub_script",
    out = f,
) for f in SPK_REQUIRED_SCRIPTS]

copy_file(
    name = "stub_start_stop_status",
    src = "@rules_synology//synology:stub_sss_script",
    out = "start-stop-status",
)

pkg_files(
    name = "scripts",
    srcs = ["stub_start_stop_status"] + [":stub_{}".format(f) for f in SPK_REQUIRED_SCRIPTS],
    attributes = pkg_attributes(
        mode = "0755",
    ),
    prefix = "scripts",
)

pkg_tar(
    name = "exampleserver",
    srcs = [
        ":conf",
        ":icons.group",
        ":info",
        ":package",
        ":scripts",
        "@rules_pkg//pkg:verify_archive_test_main.py.tpl",
    ],
    extension = "tar",
    package_file_name = "exampleserver.spk",
    visibility = ["//visibility:public"],
)
