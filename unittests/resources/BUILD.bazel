load("@bazel_skylib//rules:diff_test.bzl", "diff_test")
load("@rules_synology//:defs.bzl", "images", "info_file", "maintainer", "protocol_file", "resource_config", "service_config")
load("@bazel_skylib//rules:write_file.bzl", "write_file")

info_file(
    name = "info",
    package_name = "resource_tester",
    description = "Package to test Resources",
    maintainer = "//unittests:manny",
    os_min_ver = "7.0",
    package_version = "1.2.3-435",
)

# Consider that the "resoutce_tester" service has two ports:
#    rt-main : the port for typical service
#    rt-admin : the port for admin users to administer the administrations
#
# In this case, we need to describe:
#  resource  <-- "rez"
#    - port-config:  (entry in JSON)
#        protocol-file = {a file} <-- "file" protocol_file
#          forward from {any port} connection to {tcp/1337} to the service <-- "rt-main" service_config
#          forward from {any port} connection to {tcp/9090} to the service <-- "rt-admin" service_config
#
# This will likely find itself behind a facade or two, like a macro to simplify the number of entries

service_config(
    name = "rt-main",
    description = "Resource Tester service",
    dst_ports = "1337/tcp",
    title = "Resource Tester service port",
)

service_config(
    name = "rt-admin",
    description = "Resource Tester admin",
    dst_ports = "9090/tcp",
    title = "Resource Tester admin port",
)

protocol_file(
    name = "file",
    package_name = "rt",
    service_config = [
        ":rt-main",
        "rt-admin",
    ],
)

resource_config(
    name = "rez",
    resources = [":file"],
)

# We expect the resulting protocol file (to which a port-config points) to look like this .INI content:
write_file(
    name = "file-expected",
    out = "file_expected",
    content = [
        "[rt-main]",
        'title="Resource Tester service port"',
        'desc="Resource Tester service"',
        'port_forward="yes"',
        'dst.ports="1337/tcp"',
        "",
        "[rt-admin]",
        'title="Resource Tester admin port"',
        'desc="Resource Tester admin"',
        'port_forward="yes"',
        'dst.ports="9090/tcp"',
        "",
    ],
)

diff_test(
    name = "file_test",
    size = "small",
    file1 = ":file",
    file2 = ":file-expected",
)
